apply plugin: 'maven-publish'

def secretsFile = rootProject.file("secrets.properties")
def properties = new Properties()
properties.load(new FileInputStream(secretsFile))

def libraryGroupId = "chelo.instaleap.android."
def libraryVersion = mVersion// the version code define the library to be consumed
def libraryArtifact = getArtifactId(mId, mVariant)
def libraryPath= getArtifactFilePath(mId, mVariant)
def dependencyTree = "releaseImplementation"

def getArtifactFilePath(id, variant) {
            return "$buildDir/outputs/aar/${id}-release.aar"
}

def getArtifactId(id, variant) {
    return "${id}-release"
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId libraryGroupId
            version libraryVersion
            artifactId libraryArtifact

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.getByName(dependencyTree).allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
            artifact libraryPath
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            /** Configure path of your package repository on Github
             ** Replace GITHUB_USERID with your/organisation Github userID
             ** and REPOSITORY with the repository name on GitHub
             */
            url = uri("https://maven.pkg.github.com/cheloreyes/paho.mqtt.android")
            credentials {
                /** Create github.properties in root project folder file with
                 ** gpr.usr=GITHUB_USER_ID & gpr.key=PERSONAL_ACCESS_TOKEN
                 ** Set env variable GPR_USER & GPR_API_KEY if not adding a properties file**/

                username = properties['gpr.usr'] ?: System.getenv("GPR_USER")
                password = properties['gpr.key'] ?: System.getenv("GPR_API_KEY")
            }
        }
    }
}
//To use this use the following command:
//./gradlew -PmVersion=0.0.5 -PmVariant=debug -PmId=hermes :hermes:publish